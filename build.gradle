plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
}

group 'dev.rusthero'
version '1.0.0'

sourceCompatibility = 17
targetCompatibility = 17

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.10.1'

    testImplementation "org.mockito:mockito-core:3.+"
    testImplementation 'org.mockito:mockito-inline:3.+'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

test {
    useJUnitPlatform()
    finalizedBy(jacocoTestReport)
}

tasks.register('sourceJar', Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = 'Version Checker'
                description = 'Library to check if software is up-to-date via GitHub API.'
                url = 'https://github.com/rusthero/version-checker'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/rusthero/version-checker/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'rusthero'
                        name = 'Kara'
                        email = 'rusthero@pm.me'
                    }
                }
            }
            from components.java
        }
    }
}